include "utils/vec2.bzz"
include "utils/math.bzz"

function init(){
    simulation_initialized = 0
}

function step(){
    if (simulation_initialized != 1) {init_simulation()}

    current_cell_index = get_cell_index(pose.position)
    if (current_cell_index != last_cell_index){     # Updates the robot state and cell state once, when the robot moves out
    
        in_poi_result = is_in_poi(last_cell_index, POI_AREA)    # Check if the cell is in a POI
        if (in_poi_result.pheromone_type != nil){
            update_robot_state(in_poi_result.pheromone_type)
            
        } else {
            last_cell_state = pheromone_grid.get(last_cell_index)
            update_cell_state(last_cell_state)
        }
    }

    robot_motion()
    last_cell_index = current_cell_index
    step_count = step_count + 1

    # Debugging
    var message = string.concat(robot_state.role, ": ", robot_state.tracked_pheromone)
    debug.print(message)

    # if (id == 0){pheromone_grid.foreach(function(key, value, robot_id){log(value.intensity)})}
}

function reset(){
}

function destroy(){
}

function init_simulation(){
    ## Parameters
    math.rng.setseed(id)
    
    STEP_SIZE = 0.1     # According to the launch file
    ROBOT_VELOCITY = 80
    
    POI_AREA = {    # poi : point of interest i.e. home base or food source
        .1 = {.pheromone_type = "home_base", .center = math.vec2.new(WORLD_DATA.POI_AREA.HOME.X, WORLD_DATA.POI_AREA.HOME.Y), .radius = WORLD_DATA.POI_AREA.HOME.RADIUS},
        .2 = {.pheromone_type = "food_source", .center = math.vec2.new(WORLD_DATA.POI_AREA.GOAL.X, WORLD_DATA.POI_AREA.GOAL.Y), .radius = WORLD_DATA.POI_AREA.GOAL.RADIUS}
    }
    
    PROBE_DIRECTION_RANGE = 0.6 * math.pi
    MAX_PROBE_MAGNITUDE = 2.0
    RANDOM_NOISE_FACTOR = 0.05 * math.pi
    STEPS_BETWEEN_TURNS = 7
    RANDOM_VECTORS_NUMBER = 200

    ## Initialization
    X_CELLS_NUMBER = math.round_up(WORLD_DATA.WORLD_LENGTH / WORLD_DATA.CELL_SIZE)
    Y_CELLS_NUMBER = math.round_up(WORLD_DATA.WORLD_HEIGHT / WORLD_DATA.CELL_SIZE)
    CELLS_NUMBER = X_CELLS_NUMBER * Y_CELLS_NUMBER
    
    pheromone_grid = stigmergy.create(1)
    
    detractors_count = math.ceil(WORLD_DATA.KHEPERA_NB * WORLD_DATA.DETRACTOR_FRACTION)
    if (id <= detractors_count-1) {
        robot_state = {.role = "detractor", .tracked_pheromone = "food", .dropped_pheromone = "food", .last_poi_timestamp = 0}
        robot_global_direction = 3 * math.pi / 2.0  # Detractors facing goal
    }
    else{
        robot_state = {.role = "cooperator", .tracked_pheromone = "food", .dropped_pheromone = "home", .last_poi_timestamp = 0}
        robot_global_direction = math.rng.uniform(-math.pi, math.pi)
    }

    step_count = 0
    round_trip = 0
    out_poi = 0
    in_poi_result = {}
    last_cell_index = get_cell_index(pose.position)

    step_offset = math.rng.uniform(STEPS_BETWEEN_TURNS)

    simulation_initialized = 1
}

function move(heading_instruction){
    # Move robot at ROBOT_VELOCITY speed with heading_instruction heading.
    # Equation (3) adaptation.

    var velocity = math.vec2.newp(ROBOT_VELOCITY, heading_instruction)  # Global velocity vector
    var velocity_robot_heading = math.vec2.rotate(velocity, -pose.orientation.yaw)  # Robot velocity vector
    goto(velocity_robot_heading.x, velocity_robot_heading.y)
}

function get_cell_index(position){
    # Return the cell index. Return nil if the position is outside the world.

    var shifted_position = {}  # Consider the origin in the lower left corner for convenience

    shifted_position.x = position.x - (WORLD_DATA.WORLD_CENTER.X - WORLD_DATA.WORLD_LENGTH / 2.0)
    shifted_position.y = position.y - (WORLD_DATA.WORLD_CENTER.Y - WORLD_DATA.WORLD_HEIGHT / 2.0)

    if (shifted_position.x < 0 or shifted_position.x >= WORLD_DATA.WORLD_LENGTH or shifted_position.y < 0 or shifted_position.y >= WORLD_DATA.WORLD_HEIGHT){
        # log("WARNING get_cell_index() : Map limits exceeded")
        return nil
    } else {
        var x_index = math.round_down(shifted_position.x / WORLD_DATA.CELL_SIZE)
        var y_index = math.round_down(shifted_position.y / WORLD_DATA.CELL_SIZE)
        return (x_index + (y_index) * X_CELLS_NUMBER)
    }
}

function get_cell_position(cell_index){
    # Return the cell center position.

    var x_index = cell_index % X_CELLS_NUMBER
    var y_index = math.round_down(cell_index / Y_CELLS_NUMBER)

    var shifted_position = {}
    shifted_position.x = (x_index + 0.5) * WORLD_DATA.CELL_SIZE
    shifted_position.y = (y_index + 0.5) * WORLD_DATA.CELL_SIZE

    var position = {}
    position.x = shifted_position.x + (WORLD_DATA.WORLD_CENTER.X - WORLD_DATA.WORLD_LENGTH / 2.0)
    position.y = shifted_position.y + (WORLD_DATA.WORLD_CENTER.Y - WORLD_DATA.WORLD_HEIGHT / 2.0)

    return position
}

function is_in_poi(cell_index, poi_area){
    # Return POI type if the cell is in a POI area

    var cell_position = get_cell_position(cell_index)
    var poi_type = {}
    foreach(poi_area, function(key, area){
        if (math.vec2.dis(cell_position, area.center) <= area.radius){
            poi_type.pheromone_type = area.pheromone_type
            poi_type.center = area.center
        }
    })
    return poi_type
}

function update_robot_state(poi_type){
    # Update robot state according to the point of interest type

    if (poi_type == "home_base"){
        if (robot_state.role == "cooperator"){
            if (robot_state.dropped_pheromone == "food"){   # Counter of the number of successful round trips
                round_trip = round_trip + 1
                log("Robot: ", id, " Round trip: ", round_trip)
            }

            robot_state.last_poi_timestamp = step_count
            robot_state.tracked_pheromone = "food"
            robot_state.dropped_pheromone = "home"

        } else{
            robot_state.last_poi_timestamp = step_count 
            robot_state.tracked_pheromone = "food"  # Drop pheromone always food
        }

    } else if (poi_type == "food_source"){
        if (robot_state.role == "cooperator"){
            robot_state.last_poi_timestamp = step_count
            robot_state.tracked_pheromone = "home"
            robot_state.dropped_pheromone = "food"

        } else {
            robot_state.tracked_pheromone = "home"  # Drop pheromone always food
        }
    }
}

function update_cell_state(current_cell_state){
    # Updates vstig with the new type and intensity of pheromone in the cell.

    var new_cell_state = {}
    var steps_since_poi = step_count - robot_state.last_poi_timestamp

    if (current_cell_state != nil){
        var steps_since_last_deposit = step_count - current_cell_state.timestamp
        var cell_type = current_cell_state.type
        var cell_intensity = pheromone_evaporation(current_cell_state.intensity, steps_since_last_deposit)
    
        if (cell_type == robot_state.dropped_pheromone){   # Same pheromone, keep the maximum
            new_cell_state.type = cell_type
            new_cell_state.intensity = math.max(cell_intensity, pheromone_intensity(steps_since_poi))

        } else {    # Different pheromone, keep the new one
            new_cell_state.type = robot_state.dropped_pheromone
            new_cell_state.intensity = pheromone_intensity(steps_since_poi)
        }
    
    } else {    # If the cell is empty
        new_cell_state.type = robot_state.dropped_pheromone
        new_cell_state.intensity = pheromone_intensity(steps_since_poi)
    }
    
    pheromone_grid.put(last_cell_index, {.timestamp = step_count, .type = new_cell_state.type, .intensity = new_cell_state.intensity}) # Share timestamp so that the next one can calculate the evaporation that occurs
}

function pheromone_intensity(steps_number){
    # Return the robot pheromone intensity
    # Equation (1) implementation

    return (1000 * math.exp(- WORLD_DATA.LAMBDA * steps_number))
}

function pheromone_evaporation(intensity, steps_number){
    # Return the pheromone intensity with the evaporation that has occurred since the last deposit
    # Equation (2) implementation
    var new_intensity = intensity - WORLD_DATA.EVAPORATION_RATE * steps_number
    if (new_intensity < 0){new_intensity = 0}
    return (intensity - WORLD_DATA.EVAPORATION_RATE * steps_number)
}

function robot_motion(){
    if (in_poi_result.pheromone_type == nil){out_poi = 1}   # True if out of a POI

    limits_vector = vec_to_limits()
    if (math.vec2.length(limits_vector) < 0.5){ # Takes as direction the normal at the limit when it comes too close to it
        robot_global_direction = math.wraptopi(math.vec2.angle(limits_vector) + math.pi)
        set_leds(255,0,0)   # Red led if reflection on limits

    } else if (in_poi_result.pheromone_type != nil and out_poi){    # Takes the opposite direction when entering the POI
        # TODO : Reflection only if it's the searched POI
        robot_global_direction = math.wraptopi(robot_global_direction + math.pi)
        out_poi = 0
        set_leds(255,69,0)  # Yellow led if reflection on POI

    } else if ((step_count + step_offset) % STEPS_BETWEEN_TURNS == 0){  # Else move according to the pheromones
        var probe_index = 0
        var max_probed_intensity = 0

        # If all the probed cells are empty the last robot_global_direction is used
        while (probe_index < RANDOM_VECTORS_NUMBER){
            var probe_vector_magnitude = math.rng.uniform(MAX_PROBE_MAGNITUDE)
            var probe_vector_direction = math.rng.uniform(-PROBE_DIRECTION_RANGE, PROBE_DIRECTION_RANGE)

            var probe_vector = math.vec2.newp(probe_vector_magnitude, probe_vector_direction)
            var probe_vector_global_heading = math.vec2.rotate(probe_vector, pose.orientation.yaw)
            var probe_vector_global = math.vec2.add(probe_vector_global_heading, pose.position)
            
            var probed_cell_index = get_cell_index(probe_vector_global)
            if (probed_cell_index != nil){  # Cell is inside the world

                # TODO: Check if probed cell is in a POI
                var probed_cell_state = pheromone_grid.get(probed_cell_index)
                if (probed_cell_state != nil){  # Cell is not empty

                    var steps_since_last_deposit = step_count - probed_cell_state.timestamp
                    var probed_cell_intensity = pheromone_evaporation(probed_cell_state.intensity, steps_since_last_deposit)    # Compute intensity after evaporation
                    if (probed_cell_state.type == robot_state.tracked_pheromone){   # Cell contains the desired pheromone
                        if (probed_cell_intensity > max_probed_intensity){
                            max_probed_intensity = probed_cell_intensity
                            robot_global_direction = probe_vector_direction + pose.orientation.yaw
                        }
                    }
                }
            }
            probe_index = probe_index + 1
        }

        if (max_probed_intensity != 0){
            set_leds(0,0,255)   # Blue led if pheromone detected
        } else {
            set_leds(0,255,0)   # Green led if no pheromone detected
        }
    }
    
    robot_global_direction = robot_global_direction + math.rng.uniform(-RANDOM_NOISE_FACTOR, RANDOM_NOISE_FACTOR)   # Add noise on the direction

    if (robot_state.role == "cooperator" or (robot_state.role == "detractor" and step_count>=WORLD_DATA.T_ATTACK))
        move(robot_global_direction)
}

function vec_to_limits(){
    var vec_n = math.vec2.new(0, (WORLD_DATA.WORLD_HEIGHT / 2.0 - WORLD_DATA.WORLD_CENTER.Y) - pose.position.y)
    var vec_s = math.vec2.new(0 , - (WORLD_DATA.WORLD_HEIGHT / 2.0 - WORLD_DATA.WORLD_CENTER.Y) - pose.position.y)
    var vec_e = math.vec2.new((WORLD_DATA.WORLD_LENGTH / 2.0 - WORLD_DATA.WORLD_CENTER.X) - pose.position.x, 0)
    var vec_o = math.vec2.new(- (WORLD_DATA.WORLD_LENGTH / 2.0 - WORLD_DATA.WORLD_CENTER.X) - pose.position.x, 0)

    return math.vec2.min(math.vec2.min(vec_n, vec_s), math.vec2.min(vec_e, vec_o))
}